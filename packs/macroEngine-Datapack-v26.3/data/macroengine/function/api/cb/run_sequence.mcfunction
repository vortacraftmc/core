# macroengine:api/cb/run_sequence
# ─────────────────────────────────────────────────────────────────
# Schedules a list of commands to run sequentially, each separated
# by a configurable interval.
#
# INPUT  (storage macroengine:input cb):
#   cmds      (list of strings) — commands to run  [REQUIRED, min 1]
#   interval  (int)             — ticks between each command  [default: 2]
#   x         (int)             — CB block X  [default: 0]
#   y         (int)             — CB block Y  [default: -64]
#   z         (int)             — CB block Z  [default: 0]
#
# SECURITY: caller must hold macroengine.perm_level >= security.cmd_min_level.
#
# EXAMPLE:
#   data modify storage macroengine:input cb set value {cmds:["say one","say two","say three"],interval:20}
#   function macroengine:api/cb/run_sequence
# ─────────────────────────────────────────────────────────────────

# Security gate — see core/internal/security/check_all.
# No-op (always passes) unless flags.experimental.strict_gating is on.
scoreboard players set #macroengine.gate_ok macroengine.tmp 0
execute store result score #macroengine.gate_ok macroengine.tmp run function macroengine:core/internal/security/check_all {required:"cmd_min_level"}
execute if score #macroengine.gate_ok macroengine.tmp matches 0 run return 0

# Verify required input
execute unless data storage macroengine:input cb.cmds[0] run tellraw @s [{"translate":"macroengine.prefix.cb","color":"#00AAAA","bold":true},{"translate":"macroengine.cb.cmds_empty","color":"red"}]
execute unless data storage macroengine:input cb.cmds[0] run return 0

# Fill defaults
function macroengine:core/internal/api/cb/apply_defaults
execute unless data storage macroengine:input cb.interval run data modify storage macroengine:input cb.interval set value 2

# Expand cmds list into individual delayed queue entries
function macroengine:core/internal/systems/cb/seq_expand
data remove storage macroengine:input cb
